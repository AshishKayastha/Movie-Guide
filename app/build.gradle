apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../tools/versioning.gradle'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.ashish.movieguide"
        minSdkVersion 21
        targetSdkVersion 27
        versionName version
        versionCode buildVersionCode()

        buildConfigField 'String', 'OMDB_API_KEY', OMDB_API_KEY
        buildConfigField 'String', 'TMDB_API_KEY', TMDB_API_KEY
        buildConfigField 'String', 'TRAKT_CLIENT_ID', TRAKT_CLIENT_ID
        buildConfigField 'String', 'TRAKT_CLIENT_SECRET', TRAKT_CLIENT_SECRET
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            def keystorePropertiesFile = file("../tools/signing.properties")
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
            storeFile file(keystoreProperties['RELEASE_STORE_FILE'])
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            // Perform extra task for release build type
            if (variant.buildType.name.contains('release')) {

                // Change release file name to "Movie Guide v{versionName}.apk"
                variant.outputs.each { output ->
                    output.outputFileName = "Movie Guide v${defaultConfig.versionName}.apk"
                }

                variant.assemble.doLast {
                    def path = null
                    variant.outputs.each { output ->
                        //noinspection GrReassignedInClosureLocalVar
                        path = output.outputFile

                        // Delete unaligned apk file
                        File aligned = output.outputFile
                        File unaligned = output.packageApplication.outputFile
                        if (!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                            unaligned.delete()
                        }
                    }

                    // Show file in explorer/finder
                    exec {
                        if (System.properties['os.name'].toLowerCase().contains('windows')) {
                            ['explorer', '/select,', path].execute()
                        } else if (System.properties['os.name'].toLowerCase().contains('mac os x')) {
                            ['open', '-R', path].execute()
                        }
                    }
                }
            }
        }
    }

    lintOptions {
        xmlReport false
        warningsAsErrors true
        lintConfig file("$project.rootDir/tools/lint.xml")
        htmlOutput file("$project.buildDir/outputs/lint/lint.html")
    }

    dexOptions {
        maxProcessCount 4
        threadCount 8
    }

    sourceSets.each {
        it.java.srcDirs += "src/${it.name}/kotlin"
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

androidExtensions {
    experimental = true
}

dexcount {
    format = "json"
    verbose = false
    includeClasses = true
    includeFieldCount = false
    orderByMethodCount = true
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlinVersion"

    // Support Libraries
    implementation supportLibrary('design')
    implementation supportLibrary('customtabs')
    implementation supportLibrary('palette-v7')
    implementation supportLibrary('cardview-v7')

    // RxJava 2/RxAndroid 2
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxJava2Version"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid2Version"

    // Dagger 2
    implementation dagger('dagger')
    kapt dagger('dagger-compiler')

    // Dagger Android
    implementation dagger('dagger-android')
    implementation dagger('dagger-android-support')
    kapt dagger('dagger-android-processor')

    // Retrofit 2
    implementation retrofit('retrofit')
    implementation retrofit('converter-moshi')
    implementation retrofit('adapter-rxjava2')

    // Moshi
    implementation "com.squareup.moshi:moshi:$versions.moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$versions.moshiVersion"

    // Glide (Image Loading)
    implementation "com.github.bumptech.glide:glide:$versions.glideVersion"
    kapt "com.github.bumptech.glide:compiler:$versions.glideVersion"

    implementation "com.github.bumptech.glide:okhttp3-integration:$versions.glideVersion"

    // Thirtyinch MVP
    implementation "net.grandcentrix.thirtyinch:thirtyinch:$versions.thirtyinchVersion"

    // Requery
    implementation "io.requery:requery:$versions.requeryVersion"
    implementation "io.requery:requery-kotlin:$versions.requeryVersion"
    implementation "io.requery:requery-android:$versions.requeryVersion"
    kapt "io.requery:requery-processor:$versions.requeryVersion"

    // Timber for logging
    implementation "com.jakewharton.timber:timber:$versions.timberVersion"

    // Network Logging Interceptor
    implementation("com.github.ihsanbal:LoggingInterceptor:$versions.loggingInterceptorVersion") {
        exclude group: 'org.json', module: 'json'
    }

    // Android-State
    implementation "com.evernote:android-state:$versions.androidStateVersion"
    kapt "com.evernote:android-state-processor:$versions.androidStateVersion"

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakCanaryVersion"
}

String supportLibrary(String artifact) {
    return "com.android.support:$artifact:$versions.supportLibVersion"
}

String dagger(String artifact) {
    return "com.google.dagger:$artifact:$versions.daggerVersion"
}

String retrofit(String artifact) {
    return "com.squareup.retrofit2:$artifact:$versions.retrofitVersion"
}