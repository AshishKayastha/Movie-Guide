apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: '../tools/versioning.gradle'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        applicationId "com.ashish.movieguide"
        minSdkVersion 21
        targetSdkVersion 27
        versionName version
        versionCode buildVersionCode()

        buildConfigField 'String', 'TMDB_API_KEY', TMDB_API_KEY
        buildConfigField 'String', 'TRAKT_CLIENT_ID', TRAKT_CLIENT_ID
        buildConfigField 'String', 'TRAKT_CLIENT_SECRET', TRAKT_CLIENT_SECRET
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            def keystorePropertiesFile = file("../tools/signing.properties")
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
            storeFile file(keystoreProperties['RELEASE_STORE_FILE'])
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            // Perform extra task for release build type
            if (variant.buildType.name.contains('release')) {

                // Change release file name to "Movie Guide v{versionName}.apk"
                variant.outputs.each { output ->
                    output.outputFileName = "Movie Guide v${defaultConfig.versionName}.apk"
                }

                variant.assemble.doLast {
                    def path = null
                    variant.outputs.each { output ->
                        //noinspection GrReassignedInClosureLocalVar
                        path = output.outputFile

                        // Delete unaligned apk file
                        File aligned = output.outputFile
                        File unaligned = output.packageApplication.outputFile
                        if (!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                            unaligned.delete()
                        }
                    }

                    // Show file in explorer/finder
                    exec {
                        if (System.properties['os.name'].toLowerCase().contains('windows')) {
                            ['explorer', '/select,', path].execute()
                        } else if (System.properties['os.name'].toLowerCase().contains('mac os x')) {
                            ['open', '-R', path].execute()
                        }
                    }
                }
            }
        }
    }

    lintOptions {
        xmlReport false
        warningsAsErrors true
        lintConfig file("$project.rootDir/tools/lint.xml")
        htmlOutput file("$project.buildDir/outputs/lint/lint.html")
    }

    dexOptions {
        maxProcessCount 4
        threadCount 8
    }

    sourceSets.each {
        it.java.srcDirs += "src/${it.name}/kotlin"
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dexcount {
    format = "json"
    verbose = false
    includeClasses = true
    includeFieldCount = false
    orderByMethodCount = true
}

repositories {
    maven { url "https://clojars.org/repo/" }
}

dependencies {
    // Support Libraries
    implementation "com.android.support:design:$versions.supportLibVersion"
    implementation "com.android.support:customtabs:$versions.supportLibVersion"
    implementation "com.android.support:palette-v7:$versions.supportLibVersion"
    implementation "com.android.support:cardview-v7:$versions.supportLibVersion"

    // Timber for logging
    implementation "com.jakewharton.timber:timber:$versions.timberVersion"

    // Glide (Image Loading)
    implementation "com.github.bumptech.glide:glide:$versions.glideVersion"
    kapt "com.github.bumptech.glide:compiler:$versions.glideVersion"

    implementation "com.github.bumptech.glide:okhttp3-integration:$versions.glideVersion"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$versions.kotlinVersion"

    // Requery
    implementation "io.requery:requery:$versions.requeryVersion"
    implementation "io.requery:requery-kotlin:$versions.requeryVersion"
    implementation "io.requery:requery-android:$versions.requeryVersion"
    kapt "io.requery:requery-processor:$versions.requeryVersion"

    // Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:$versions.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$versions.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofitVersion"

    // Network Logging Interceptor
    implementation("com.github.ihsanbal:LoggingInterceptor:$versions.loggingInterceptorVersion") {
        exclude group: 'org.json', module: 'json'
    }

    // RxJava 2/RxAndroid 2
    implementation "io.reactivex.rxjava2:rxjava:$versions.rxJava2Version"
    implementation "io.reactivex.rxjava2:rxandroid:$versions.rxAndroid2Version"

    // Dagger 2
    implementation "com.google.dagger:dagger:$versions.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$versions.daggerVersion"

    // IcePick
    implementation "frankiesardo:icepick:$versions.icepickVersion"
    kapt "frankiesardo:icepick-processor:$versions.icepickVersion"

    // Paper Parcel
    implementation "nz.bradcampbell:paperparcel:$versions.paperParcelVersion"
    kapt "nz.bradcampbell:paperparcel-compiler:$versions.paperParcelVersion"

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$versions.leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$versions.leakCanaryVersion"

    // Stetho
    debugImplementation "com.facebook.stetho:stetho:$versions.stethoVersion"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$versions.stethoVersion"
}