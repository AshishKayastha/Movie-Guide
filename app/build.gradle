apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.ashish.movies"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 100
        versionName "1.0.0"

        buildConfigField 'String', 'TMDB_API_KEY', TMDB_API_KEY
    }

    signingConfigs {
        release {
            def keystoreProperties = new Properties()
            def keystorePropertiesFile = file("../tools/signing.properties")
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

            keyAlias keystoreProperties['RELEASE_KEY_ALIAS']
            keyPassword keystoreProperties['RELEASE_KEY_PASSWORD']
            storePassword keystoreProperties['RELEASE_STORE_PASSWORD']
            storeFile file(keystoreProperties['RELEASE_STORE_FILE'])
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        applicationVariants.all { variant ->
            // Perform extra task for release build type
            if (variant.buildType.name.contains('release')) {

                // Change release file name to "Movies v{versionName}.apk"
                variant.outputs.each { output ->
                    def apk = output.outputFile;
                    def newName = "Movies v" + defaultConfig.versionName + ".apk";
                    output.outputFile = new File(apk.parentFile, newName);
                }

                variant.assemble.doLast {
                    def path = null;
                    variant.outputs.each { output ->
                        //noinspection GrReassignedInClosureLocalVar
                        path = output.outputFile

                        // Delete unaligned apk file
                        File aligned = output.outputFile
                        File unaligned = output.packageApplication.outputFile;
                        if (!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                            unaligned.delete()
                        }
                    }

                    // Show file in explorer/finder
                    exec {
                        if (System.properties['os.name'].toLowerCase().contains('windows')) {
                            ['explorer', '/select,', path].execute()
                        } else if (System.properties['os.name'].toLowerCase().contains('mac os x')) {
                            ['open', '-R', path].execute()
                        }
                    }
                }
            }
        }
    }

    lintOptions {
        xmlReport false
        warningsAsErrors true
        lintConfig file("$project.rootDir/tools/lint.xml")
        htmlOutput file("$project.buildDir/outputs/lint/lint.html")
    }

    dexOptions {
        maxProcessCount 4
        threadCount 8
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dexcount {
    format = "json"
    verbose = false
    includeClasses = true
    includeFieldCount = false
    orderByMethodCount = true
}

repositories {
    maven { url "https://clojars.org/repo/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    // Support Libraries
    compile "com.android.support:design:$supportLibVersion"
    compile "com.android.support:customtabs:$supportLibVersion"
    compile "com.android.support:palette-v7:$supportLibVersion"
    compile "com.android.support:cardview-v7:$supportLibVersion"

    // Timber for logging
    compile "com.jakewharton.timber:timber:$timberVersion"

    // KotterKnife
    compile "com.jakewharton:kotterknife:$kotterKnifeVersion"

    // Glide (Image Loading)
    compile "com.github.bumptech.glide:glide:$glideVersion"

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // StorIO
    compile "com.pushtorefresh.storio:sqlite:$storioVersion"
    compile "com.pushtorefresh.storio:sqlite-annotations:$storioVersion"
    kapt "com.pushtorefresh.storio:sqlite-annotations-processor:$storioVersion"

    // Retrofit 2
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // OkHttp
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // RxJava 2/RxAndroid 2
    compile "io.reactivex.rxjava2:rxjava:$rxJava2Version"
    compile "io.reactivex.rxjava2:rxandroid:$rxAndroid2Version"
    compile "com.github.akarnokd:rxjava2-interop:$rxJava2InteropVersion"

    // Dagger 2
    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    // IcePick
    compile "frankiesardo:icepick:${icepickVersion}"
    kapt "frankiesardo:icepick-processor:${icepickVersion}"

    // Paper Parcel
    compile "nz.bradcampbell:paperparcel:$paperParcelVersion"
    kapt "nz.bradcampbell:paperparcel-compiler:$paperParcelVersion"

    // Leak Canary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
}